title Wunsz 0.4.2
author OmumbOrOmbOngO
homepage https://kawabart.itch.io/wunsz
noaction

verbose_logging

========
OBJECTS
========

Background
green #4a941c
00000
00010
00000
01000
00001

Wall
brown

Player
black yellow
00000
00000
01010
00000
00000

Tail
blue lightgreen
00011
10001
11000
10001
00011

Apple
red black
..1..
.000.
00000
00000
.000.

Exit
yellow lightgreen
.111.
10001
10001
10001
.111.

Blockade
#261703 darkbrown
.000.
01010
00100
01010
.000.

CannotMoveHorizontally
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player
T = Tail
ó = Apple
B = Blockade
X = Exit
& = Blockade and Exit
Q = Player and Exit

Obstacle = Tail or Wall or Blockade

=======
SOUNDS
=======

Tail destroy 42814708 ( level completion animation )
Apple destroy 11296707 ( eating )
SFX4 70561304 ( player stuck )

================
COLLISIONLAYERS
================

CannotMoveHorizontally
Background
Exit
Player, Tail, Wall, Apple, Blockade

======
RULES
======

( player eats apple -> unlock exit )
[ > Player | Apple ] [ Exit ] -> [ > Player | No Apple ] [ Exit No Blockade ]

[ Tail | Player ] -> [ > Tail | Player ]
[ Player Exit ] -> [ Exit ]

[ stationary Tail | moving Tail ] -> [ > Tail | moving Tail ] again
+ [ Tail | Tail Exit ] -> [ > Tail | Exit ] again
+ [ Tail Exit ] -> [ Exit ]

( checking if the player is stuck )
late horizontal [ Obstacle | Player No Exit | Obstacle ] -> [ Obstacle | Player CannotMoveHorizontally | Obstacle ]
late vertical [ Obstacle | Player CannotMoveHorizontally | Obstacle ] -> SFX4
[ CannotMoveHorizontally ] -> [ ]

==============
WINCONDITIONS
==============

No Player
No Tail on Exit

=======
LEVELS
=======

#########
#TTP.ó.&#
#########

########
#......#
#......#
#....ó.#
#..P...#
#.TT...#
#TT...&#
########

###########
#........&#
#...ttt...#
#...pót...#
#...ttt...#
#.........#
###########

###########
#....ptt.&#
#...###t..#
#.#.#ó.t#.#
#.#.##.t.##
#..#..#ttt#
#..#..#####
#.....ó..##
#.#.#.#...#
#...#...#.#
###########

###########
#........X#
#...T.T...#
#....ó....#
#..t...t..#
#...ttt...#
#P........#
###########

#######
#TTTTT#
#TTTTT#
#TTQTT#
#TTTTT#
#TTTTT#
#######

message End of demo.
message Thanks for slithering :>
